struct {
uint offsetEvents<format=hex>;
uint eventCount;
uint offsetCharSet<format=hex>;
uint charCount;
uint offsetCharGraphs<format=hex>;
uint charGraphsCount;
uint offsetUnknown<format=hex>;
uint unknownCount;
uint offsetUsedEvents<format=hex>;
uint usedEventCount;
} header;

typedef struct {
    ushort code;
    short positionOffset;
}letter<read=letter_Read>;


FSeek(header.offsetCharSet);
struct {
    short uA;
    wchar_t c;
    uint index;
} chars[header.charCount];

wstring letter_Read(letter &let) {
    wstring s = L"";
    if(let.code <= 0x8000) {
        s += chars[let.code].c;
    } else {
        s += L' ';
    }
    return s;
}

FSeek(header.offsetEvents);
local int i;
local int j;
local uint pos;
local uint pos2;
local uint pos3;
struct {
    uint paragraphsOffset<format=hex>;
    uint paragraphCount;
    uint uA;
    uint eventId<format=hex>;
    pos = FTell();
    FSeek(paragraphsOffset);
    for(i = 0; i <paragraphCount; i++) {    
        struct {
            uint stringsOffset<format=hex>;
            int stringCount;
            int u_b;
            int u_c;
            short u_d;
            short u_e;
            pos2 = FTell();
            FSeek(stringsOffset);
            for(j = 0; j <stringCount; j++) {
                struct strStruct{
                    uint stringOffset<format=hex>;
                    uint u_a;
                    uint u_b;
                    uint length;
                    float u_d;
                    uint u_e;
                    pos3 = FTell();
                    FSeek(stringOffset);
                    letter letters[(length-1)/2];
                    ushort terminator;
                    FSeek(pos3);
                }str<read=read_strStruct>;
            }
            FSeek(pos2);
        } paragraph;
    }
    FSeek(pos);
} events[header.eventCount]<optimize=false>;

wstring read_strStruct(strStruct &str) {
    wstring s = L"";
    int i = 0;
    for(i = 0; i < (str.length - 1)/2; i++) {
        if(str.letters[i].code == 0x8001) {
            s += L' ';
        } else if (str.letters[i].code == 0x8003) {
            s += L"<LeftStick>";
        } else {
            s += chars[str.letters[i].code].c;
        }
    }
    return s;
}

FSeek(header.offsetCharGraphs);
struct {
    uint textureID<format=hex>;
    float u1;
    float v1;
    float u2;
    float v2;
    float width;
    float height;
    float unknown[3];
} charGraphs[header.charGraphsCount];

FSeek(header.offsetUnknown);
struct {
    uint u_a;
    float u_b;
    float u_c;
    float u_d;
    uint u_e;
} unknown[header.unknownCount];

FSeek(header.offsetUsedEvents);
struct {
    uint eventID<format=hex>;
    uint envenIndex;
    char name[32];
} usedEvents[header.usedEventCount];